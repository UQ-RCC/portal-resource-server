buildscript {
	repositories {
		mavenCentral()
	}

	dependencies {
		classpath 'org.ajoberstar:grgit:1.1.0'
	}
}

plugins {
	id 'java'
	id 'application'
	id 'distribution'
	id 'fr.brouillard.oss.gradle.jgitver' version '0.8.0'
}

jgitver {
	useDistance true
	useGitCommitID true
	useDirty true
	autoIncrementPatch false
}

repositories {
	mavenCentral()
}

mainClassName = 'au.edu.uq.rcc.portal.resource.ResourceServerApplication'

ext {
	SPRING_BOOT_VERSION     = '2.4.3'
	SPRING_SECURITY_VERSION = '5.5.2'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

dependencies {
	implementation group: 'org.apache.logging.log4j', name: 'log4j-1.2-api', version: '2.15.0'

	implementation group: 'com.kstruct', name: 'gethostname4j', version: '0.0.3'

	/* Spring dependencies */
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-tomcat', version: SPRING_BOOT_VERSION
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: SPRING_BOOT_VERSION
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: SPRING_BOOT_VERSION
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: SPRING_BOOT_VERSION

	implementation group: 'org.springframework.security', name: 'spring-security-core', version: SPRING_SECURITY_VERSION
	implementation group: 'org.springframework.security', name: 'spring-security-oauth2-jose', version: SPRING_SECURITY_VERSION
	implementation group: 'org.springframework.security', name: 'spring-security-oauth2-resource-server', version: SPRING_SECURITY_VERSION

	/* Apache Commons Configuration dependencies */
	implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.1'
	implementation group: 'org.apache.commons', name: 'commons-exec', version: '1.3'
	implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'

	/* Bouncycastle */
	implementation group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.64'
	implementation group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.64'

	testImplementation 'junit:junit:4.12'

	implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '+'
}

distributions {
	main {
		distZip.enabled false
		contents {
			eachFile {
				if (it.relativePath.segments[-2] == 'lib') {
					it.mode = 0644
				} else if (it.relativePath.segments[-2] == 'bin') {
					it.mode = 0755
				}
			}

			into("etc/${project.name}") {
				from('application.sample.yml')
				rename('application.sample.yml', 'application.yml')
				fileMode 0644
			}

			into("etc/${project.name}") {
				from('logback-spring.xml')
				fileMode 0644
			}
		}
	}

	deb {
		debDistZip.enabled false
		debDistTar.enabled false
		contents {
			into("usr/share/${project.name}") {
				with distributions.main.contents
			}

			into("etc/${project.name}") {
				from('application.sample.yml')
				rename('application.sample.yml', 'application.yml')
				fileMode 0644
			}

			into("etc/${project.name}") {
				from('logback-spring.xml')
				fileMode 0644
			}
		}
	}
}

distTar {
	compression Compression.GZIP
	extension 'tar.gz' /* Not .tgz */
}

installDebDist {
	destinationDir = new File("debian/${project.name}")
}
